Class RepeaterZombie : Actor
{
  bool firing;
  int heat;

  Default
  {
    Health 100;
    Radius 20;
    Height 56;
    Speed 8;
    PainChance 96;
    DropItem "CellPack"; //DropItem "Repeater", 256, 20
    SeeSound "Grunt/Sight";
    PainSound "Grunt/Pain";
    DeathSound "Grunt/Death";
    ActiveSound "Grunt/Active";
    Obituary "%o was vapourized by a Zombie with a Repeater" ;
    Decal "MummyScorch";
    Monster;
    +FloorClip
  }

  States
  {
  Spawn:
    RPZM A 0 A_JumpIf(firing, "SpinDown");
    RPZM A 0 { bNoPain = false; }
    RPZM AAAAAAAAAABBBBBBBBBB 1 A_Look();
    Loop;
  See:
    RPZM A 0 A_JumpIf(firing, "SpinDown");
    RPZM A 0 { bNoPain = false; }
    RPZM AABBCCDD 4 A_Chase();
    Loop;
  Missile:
    RPZM E 4
    {
      bNoPain = true;
      firing = true;
      A_PlaySound("Weapons/RepeaterSpinUp", CHAN_7);
      A_FaceTarget();
    }
    RPZM EFFEFEF 4 A_FaceTarget();
  Firing:
    RPZM E 0 A_JumpIf(heat>=20, "Overload");
    RPZM E 0 { heat++; }
    RPZM G 2 Bright RepeaterFire();
    RPZM F 2;
    RPZM H 2 Bright RepeaterFire();
    RPZM E 2;
    RPZM E 0 A_Jump(220, "Firing");
    RPZM E 0 A_SpidRefire();
    Loop;
  SpinDown:
    RPZM E 4
    {
      A_PlaySound("Weapons/RepeaterSpinDown", CHAN_7);
      firing = false;
      heat = 0;
    }
    RPZM FEFEEFF 4;
    Goto See;
  Overload:
    RPZM E 0
    {
      A_PlaySound("Weapons/RepeaterSpinDown", CHAN_7);
      A_PlaySound("RepeaterZombie/Steam", CHAN_WEAPON);
      firing=false;
      heat=0;
    }
    RPZM EEEEEFFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    RPZM EEEEEEFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    RPZM EEEEEFFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    RPZM EEEEEEFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    RPZM EEEEEFFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    RPZM EEEEEEFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    RPZM EEEEEFFFFFF 1 A_SpawnItemEx("RepeaterZombieSteam", -5, Random(12, 14), Random(31, 33), 4, 0, (0.1)*Random(-5, 5), Random(85, 90), SXF_CLIENTSIDE);
    Goto See;
  Pain:
    RPZM I 0 A_StopSound(CHAN_7);
    RPZM I 3;
    RPZM I 3 A_Pain();
    Goto See;
  Death:
    RPZM I 4 A_StopSound(CHAN_7);
    RPZM J 5;
    RPZM K 5 A_Scream();
    RPZM L 5 A_Fall();
    RPZM M 5;
    RPZM N -1;
    Stop;
  XDeath:
    RPZM O 5 A_StopSound(CHAN_7);
    RPZM P 5 A_XScream();
    RPZM Q 5;
    RPZM R 5 A_Fall();
    RPZM STUV 5;
    RPZM W -1;
    Stop;
  Raise:
    RPZM NMLKJI 5;
    Goto See;
  }

  void RepeaterFire()
  {
    A_PlaySound("Weapons/RepeaterFire");
    A_CustomBulletAttack(12, 4, 2, 5, "RepeaterZombiePuff");
  }
}

Class RepeaterZombiePuff : Actor
{
  Default
  {
    Radius 3;
    Height 3;
    RenderStyle "Add";
    Alpha 0.75;
    +AlwaysPuff
    +PuffOnActors
    +NoGravity
    +NoBlockMap
    +BloodlessImpact
    -BloodSplatter
  }

  States
  {
  Spawn:
    RPUF A 1 Bright;
    RPUF A 0 Bright A_PlaySound("Weapons/RepeaterHit");
    RPUF A 2 Bright;
    RPUF BCDEFG 3 Bright;
    Stop;
  }
}

Class RepeaterZombieSteam : Actor
{
  Default
  {
    Height 1;
    Radius 1;
    Speed 4;
    RenderStyle "Add";
    Scale 0.3;
    Alpha 0.65;
    +Missile
    +NoGravity
    +NoBlockMap
    +BloodlessImpact
    -BloodSplatter
  }

  States
  {
  Spawn:
    STEM A 1 A_SetTranslucent(0.6, 1);
    STEM B 2 A_SetTranslucent(0.55, 1);
    STEM C 1 A_SetTranslucent(0.5, 1);
    STEM D 2 A_SetTranslucent(0.45, 1);
    STEM E 1 A_SetTranslucent(0.4, 1);
    STEM F 2 A_SetTranslucent(0.35, 1);
    STEM G 1 A_SetTranslucent(0.3, 1);
    STEM H 2 A_SetTranslucent(0.25, 1);
    STEM I 1 A_SetTranslucent(0.2, 1);
    STEM J 2 A_SetTranslucent(0.15, 1);
    STEM K 1 A_SetTranslucent(0.1, 1);
    STEM L 2 A_SetTranslucent(0.05, 1);
    Stop;
  }
}
